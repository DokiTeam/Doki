import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'com.android.application'
	id 'kotlin-android'
	id 'com.google.devtools.ksp'
	id 'kotlin-parcelize'
	id 'dagger.hilt.android.plugin'
	id 'androidx.room'
	id 'org.jetbrains.kotlin.plugin.serialization'
	// enable if needed
	// id 'dev.reformator.stacktracedecoroutinator'
}

android {
	compileSdk = 36
	buildToolsVersion = '36.0.0'
	namespace = 'org.dokiteam.doki'

	defaultConfig {
		applicationId 'org.dokiteam.doki'
		minSdk = 21
		//noinspection ExpiredTargetSdkVersion
		targetSdk = 22

        versionCode = 1
        versionName "1.0"

		generatedDensities = []
		testInstrumentationRunner 'org.dokiteam.doki.HiltTestRunner'
		ksp {
			arg('room.generateKotlin', 'true')
		}
		androidResources {
			generateLocaleConfig false
		}
		def localProperties = new Properties()
		def localPropertiesFile = rootProject.file('local.properties')
		if (localPropertiesFile.exists()) {
			localProperties.load(new FileInputStream(localPropertiesFile))
		}

        // Put your GitHub username in this flag when building for in-app update feature
        // -Dgh_username=
        def ghUser = System.getProperty("gh_username")
            ?: localProperties.getProperty("gh_username", "")
        resValue "string", "gh_username", ghUser

        // Put your Telegram bot token in this flag when building for Telegram backup feature
        // -Dtg_backup_bot_token=
        def tgKey = System.getProperty("tg_backup_bot_token")
            ?: localProperties.getProperty("tg_backup_bot_token", "")
        resValue 'string', 'tg_backup_bot_token', tgKey
    }
    signingConfigs {
        DokiApp {
            storeFile file("sign.p12")
            storePassword "kotatsuanddokiarethebest"
            keyAlias "DokiApp"
            keyPassword "kotatsuanddokiarethebest"
            storeType "PKCS12"
        }
    }
	buildTypes {
		debug {
			applicationIdSuffix = '.debug'
		}
		release {
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.DokiApp
		}
        legacy {
            applicationIdSuffix = '.legacy'
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.DokiApp
        }
	}
	buildFeatures {
		viewBinding true
		buildConfig true
	}
	packagingOptions {
		resources {
			excludes += [
				'META-INF/README.md',
				'META-INF/NOTICE.md'
			]
		}
        jniLibs {
            useLegacyPackaging = true
        }
	}
	sourceSets {
		androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
		main.java.srcDirs += 'src/main/kotlin/'
	}
	compileOptions {
		coreLibraryDesugaringEnabled true
		sourceCompatibility JavaVersion.VERSION_17
		targetCompatibility JavaVersion.VERSION_17
	}
	lint {
        checkReleaseBuilds false
		abortOnError true
		disable 'MissingTranslation', 'PrivateResource', 'SetJavaScriptEnabled', 'SimpleDateFormat'
	}
	testOptions {
		unitTests.includeAndroidResources true
		unitTests.returnDefaultValues false
	}
    applicationVariants.configureEach { variant ->
        def now = LocalDateTime.now()
        if (variant.name == 'legacy') {
            variant.outputs.each { output ->
                output.versionCodeOverride = now.format("yyMMdd").toInteger()
                output.versionNameOverride = 'C' + now.format("yyMMdd")
            }
        } else if (variant.name == 'release') {
            variant.outputs.each { output ->
                output.versionCodeOverride = now.format("yyMMdd").toInteger()
                output.versionNameOverride = 'P' + now.format("yyMMdd")
            }
        }
    }
}
kotlin {
    compilerOptions {
        jvmTarget = JvmTarget.JVM_17
        freeCompilerArgs.addAll([
            '-opt-in=kotlin.ExperimentalStdlibApi',
            '-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
            '-opt-in=kotlinx.coroutines.ExperimentalForInheritanceCoroutinesApi',
            '-opt-in=kotlinx.coroutines.InternalForInheritanceCoroutinesApi',
            '-opt-in=kotlinx.coroutines.FlowPreview',
            '-opt-in=kotlin.contracts.ExperimentalContracts',
            '-opt-in=coil3.annotation.ExperimentalCoilApi',
            '-opt-in=coil3.annotation.InternalCoilApi',
            '-opt-in=kotlinx.serialization.ExperimentalSerializationApi',
            '-Xjspecify-annotations=strict',
            '-Xannotation-default-target=first-only',
            '-Xtype-enhancement-improvements-strict-mode'
        ])
        room {
            schemaDirectory "$projectDir/schemas"
        }
    }
}
tasks.withType(KotlinCompile).configureEach {
    compilerOptions {
        freeCompilerArgs.add('-opt-in=org.dokiteam.doki.parsers.InternalParsersApi')
    }
}
dependencies {
    def exts = libs.versions.exts.get()
    if (System.properties.containsKey('exts')) {
        // Use this flag with build command for auto-fetch and replace:
        // -Dexts=$(curl -s https://api.github.com/repos/DokiTeam/doki-exts/commits/base -H "Accept: application/vnd.github.sha" | cut -c -10)
        exts = System.getProperty('exts')
    }
    //noinspection UseTomlInstead
    implementation("com.github.DokiTeam:doki-exts:$exts") {
        exclude group: 'org.json', module: 'json'
    }

	coreLibraryDesugaring libs.desugar.jdk.libs
	implementation libs.kotlin.stdlib
	implementation libs.kotlinx.coroutines.android
	implementation libs.kotlinx.coroutines.guava

	implementation libs.androidx.appcompat
	implementation libs.androidx.core
	implementation libs.androidx.activity
	implementation libs.androidx.fragment
	implementation libs.androidx.transition
	implementation libs.androidx.collection
	implementation libs.lifecycle.viewmodel
	implementation libs.lifecycle.service
	implementation libs.lifecycle.process
	implementation libs.androidx.constraintlayout
	implementation libs.androidx.documentfile
	implementation libs.androidx.swiperefreshlayout
	implementation libs.androidx.recyclerview
	implementation libs.androidx.viewpager2
	implementation libs.androidx.preference
	implementation libs.androidx.biometric
	implementation libs.material
	implementation libs.androidx.lifecycle.common.java8
	implementation libs.androidx.webkit

	implementation libs.androidx.work.runtime
	implementation libs.guava

    // Foldable/Window layout
    implementation libs.androidx.window

	implementation libs.androidx.room.runtime
	implementation libs.androidx.room.ktx
	ksp libs.androidx.room.compiler

	implementation libs.okhttp
	implementation libs.okhttp.tls
	implementation libs.okhttp.dnsoverhttps
	implementation libs.okio
	implementation libs.kotlinx.serialization.json

	implementation libs.adapterdelegates
	implementation libs.adapterdelegates.viewbinding

	implementation libs.hilt.android
	ksp libs.hilt.compiler
	implementation libs.androidx.hilt.work
	ksp libs.androidx.hilt.compiler

	implementation libs.coil.core
	implementation libs.coil.network
	implementation libs.coil.gif
	implementation libs.coil.svg
	implementation libs.avif.decoder
	implementation libs.ssiv
	implementation libs.disk.lru.cache
	implementation libs.markwon
	implementation libs.kizzyrpc

	implementation libs.acra.http
	implementation libs.acra.dialog

	implementation libs.conscrypt.android

	debugImplementation libs.leakcanary.android
	debugImplementation libs.workinspector

	testImplementation libs.junit
	testImplementation libs.json
	testImplementation libs.kotlinx.coroutines.test

	androidTestImplementation libs.androidx.runner
	androidTestImplementation libs.androidx.rules
	androidTestImplementation libs.androidx.test.core
	androidTestImplementation libs.androidx.junit

	androidTestImplementation libs.kotlinx.coroutines.test

	androidTestImplementation libs.androidx.room.testing
	androidTestImplementation libs.moshi.kotlin

	androidTestImplementation libs.hilt.android.testing
	kspAndroidTest libs.hilt.android.compiler
}
